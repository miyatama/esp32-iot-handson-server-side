plugins {
  id 'java'
  id 'application'
  id 'net.ltgt.errorprone' version '0.6.1'
  id 'com.github.sherter.google-java-format' version '0.8'
}

ext {
  kafkaVersion = '3.1.0'
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  implementation group: 'org.json', name: 'json', version: '20220320'
  implementation 'com.google.guava:guava:28.2-jre'
  implementation "org.apache.kafka:kafka-streams:${kafkaVersion}"
  implementation 'org.slf4j:slf4j-api:1.7.3'
  implementation 'ch.qos.logback:logback-classic:1.2.3'
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation 'io.javalin:javalin:3.7.0'
  implementation 'com.squareup.okhttp3:okhttp:4.3.1'

  errorprone 'com.google.errorprone:error_prone_core:2.3.2'
  errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

application {
  mainClassName = 'com.example.summarizer.App'
}

test {
  useJUnitPlatform()
}

googleJavaFormat {
  source = sourceSets*.allJava
  include '**/*.java'
  exclude 'src/test/*'
}

tasks.withType(JavaCompile).configureEach { javaCompile ->
  javaCompile.dependsOn 'googleJavaFormat'
  options.compilerArgs << "-Xlint:deprecation"
  options.encoding = 'UTF-8'
}

task runStream(type: JavaExec) {
    main = 'com.example.summarizer.App'
    classpath sourceSets.main.runtimeClasspath
}
